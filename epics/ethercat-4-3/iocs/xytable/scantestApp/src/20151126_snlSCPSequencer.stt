
program snlSCPSequencer
%{#include <time.h>}%
%{#include <string.h>}%
%{#include <math.h>}%
%{#include<stdio.h>}%
%{#include<stdlib.h>}%

// IOC variables
//////////////////////////////////////////
double snlState;
assign snlState to "{device}:snlState";
monitor snlState;

double encCalc;
assign encCalc to "{device}:encCalc";
monitor encCalc;

double encCalc_ENG;
assign encCalc_ENG to "{device}:encCalc_ENG";
monitor encCalc_ENG;

double potChVal;
assign potChVal to "{device}:potChVal";
monitor potChVal;

double potChVal_ENG;
assign potChVal_ENG to "{device}:potChVal_ENG";
monitor potChVal_ENG;

double snlCLFeedback;
assign snlCLFeedback to "{device}:snlCLFeedback";
monitor snlCLFeedback;

double dist_Encoder;
assign dist_Encoder to "{device}:dist_Encoder";
monitor dist_Encoder;

double dist_Pot;
assign dist_Pot to "{device}:dist_Pot";
monitor dist_Pot;

double snlFailSafe;
assign snlFailSafe to "{device}:snlFailSafe";
monitor snlFailSafe;

double lowerLim;
assign lowerLim to "{device}:lowerLim";
monitor lowerLim;

double isCalibrated;
assign isCalibrated to "{device}:isCalibrated";
monitor isCalibrated;

double CL_enable;
assign CL_enable to "{device}:CL_enable";

double jog_enable;
assign jog_enable to "{device}:jog_enable";

double fanout_snlState_val;
assign fanout_snlState_val to "{device}:fanout_snlState_val";

double fanout_snlCLFeedback_val;
assign fanout_snlCLFeedback_val to "{device}:fanout_snlCLFeedback_val";

double CL_setpoint_dist;
assign CL_setpoint_dist to "{device}:CL_setpoint_dist";

string CL_FBSensor;
assign CL_FBSensor to "{device}:CL_FBSensor";

string CL_FSSensor;
assign CL_FSSensor to "{device}:CL_FSSensor";

double FSErrorLim_val;
assign FSErrorLim_val to "{device}:FSErrorLim_val";

ss ss1 {
    state PowerOnReset_delay_state {
		when(delay(0.5)){
		} state PowerOnReset_state
	}
	state PowerOnReset_state {
		option -e;
		entry { 
		   printf("Entry PowerOnReset_state\n\r");
		   snlState = 0;
		   pvPut(snlState,SYNC);
		   CL_enable = 0;  // control loop is not enabled
		   pvPut(CL_enable,SYNC);
		   jog_enable = 0;  // jog is not enabled
		   pvPut(jog_enable,SYNC);
		   fanout_snlState_val = 1; // in power on reset mode
		   pvPut(fanout_snlState_val,SYNC);
		   printf("CL DISABLED | JOG DISABLED | FANOUT 1\n\r");
		}
		when (snlState==1) {	
			printf("##Requested CL_state\n\r");
			isCalibrated = 0;
			pvPut(isCalibrated,SYNC);
			calibrate();
		} state CL_delay_state
		when (snlState==2) {	
			printf("##Requested OL_state\n\r");
			isCalibrated = 0;
			pvPut(isCalibrated,SYNC);
			calibrate();
		} state OL_delay_state
    }
	state CL_delay_state {
		when(delay(0.5)){
		} state CL_state
	}
	state CL_state {
		option -e;
		entry { 
		   printf("Entry CL_state\n\r");
		   snlState = 1;
		   pvPut(snlState,SYNC);
		   CL_enable = 1;  // control loop is enabled
		   pvPut(CL_enable,SYNC);
		   jog_enable = 0;  // jog is not enabled
		   pvPut(jog_enable,SYNC);
		   fanout_snlState_val = 2; // in closed loop mode
		   pvPut(fanout_snlState_val,SYNC);
		   printf("CL ENABLED | JOG DISABLED | FANOUT 2\n\r");
		}
		when (snlState==0) {	
			printf("##Requested PowerOnReset_state\n\r");
			CL_enable = 0;  // control loop is not enabled
			pvPut(CL_enable,SYNC);
		} state PowerOnReset_delay_state
		when (snlState==2) {	
			printf("##Requested OL_state\n\r");
			CL_enable = 0;  // control loop is not enabled
			pvPut(CL_enable,SYNC);
			//calibrate();
		} state OL_delay_state
    }
	state OL_delay_state {
		when(delay(0.5)){
		} state OL_state
	}
	state OL_state {
		option -e;
		entry { 
		   printf("Entry OL_state\n\r");
		   snlState = 2;
		   pvPut(snlState,SYNC);
		   CL_enable = 0;  // control loop is not enabled
		   pvPut(CL_enable,SYNC);
		   jog_enable = 1;  // jog is not enabled
		   pvPut(jog_enable,SYNC);
		   fanout_snlState_val = 3; // in open loop mode
		   pvPut(fanout_snlState_val,SYNC);
		   printf("CL DISABLED | JOG ENABLED | FANOUT 3\n\r");
		}
		when (snlState==0) {	
			printf("##Requested PowerOnReset_state\n\r");
			jog_enable = 0;  // jog is not enabled
			pvPut(jog_enable,SYNC);
		} state PowerOnReset_delay_state
		when (snlState==1) {	
			printf("##Requested CL_state\n\r");
			jog_enable = 0;  // jog is not enabled
			pvPut(jog_enable,SYNC);
			//calibrate();
			set_CL_setpoint_dist();
		} state CL_delay_state
    }
}

void calibrate()
{
   if(snlCLFeedback==0){	// Mechanical Encoder
		if(snlFailSafe==0)	// mechanical encoder
		{
			strcpy(CL_FSSensor, "MECHANICAL ENCODER");
			pvPut(CL_FSSensor,SYNC);
			FSErrorLim_val = 1;
			pvPut(FSErrorLim_val,SYNC);
		}
		else if(snlFailSafe==1)	// potentiometer
		{
			strcpy(CL_FSSensor, "POTENTIOMETER");
			pvPut(CL_FSSensor,SYNC);
			FSErrorLim_val = 2;
			pvPut(FSErrorLim_val,SYNC);
		}
		else 	// limit switch
		{
		   /* executes when the none of the above condition is true */
		}
		// set actual and dist setpoints
		CL_setpoint_dist = encCalc_ENG;
		pvPut(CL_setpoint_dist, SYNC);
		// choose feedback sensor
		fanout_snlCLFeedback_val = 1;
		pvPut(fanout_snlCLFeedback_val, SYNC);
		// show on CSS GUI
		strcpy(CL_FBSensor, "MECHANICAL ENCODER");
		pvPut(CL_FBSensor,SYNC);
	}
	if(snlCLFeedback==1){	// Potentiometer
		if(snlFailSafe==0)	// mechanical encoder
		{
			strcpy(CL_FSSensor, "MECHANICAL ENCODER");
			pvPut(CL_FSSensor,SYNC);
			FSErrorLim_val = 2;
			pvPut(FSErrorLim_val,SYNC);
		}
		else if(snlFailSafe==1)	// potentiometer
		{
			strcpy(CL_FSSensor, "POTENTIOMETER");
			pvPut(CL_FSSensor,SYNC);
			FSErrorLim_val = 1;
			pvPut(FSErrorLim_val,SYNC);
		}
		else 	// limit switch
		{
		   /* executes when the none of the above condition is true */
		}
		// set actual and dist setpoints
		CL_setpoint_dist = potChVal_ENG;
		pvPut(CL_setpoint_dist, SYNC);
		// choose feedback sensor
		fanout_snlCLFeedback_val = 2;
		pvPut(fanout_snlCLFeedback_val, SYNC);
		// show on CSS GUI
		strcpy(CL_FBSensor, "POTENTIOMETER");
		pvPut(CL_FBSensor,SYNC);
	}
	isCalibrated = 1;
	pvPut(isCalibrated, SYNC);
}

void set_CL_setpoint_dist(){
	if(snlCLFeedback==0){	// Mechanical Encoder
		CL_setpoint_dist = encCalc_ENG;
		pvPut(CL_setpoint_dist, SYNC);
	}
	else if(snlCLFeedback==1){	// Potentiometer
		CL_setpoint_dist = potChVal_ENG;
		pvPut(CL_setpoint_dist, SYNC);
	}
}
